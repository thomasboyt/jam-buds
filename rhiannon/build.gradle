buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.flywaydb.flyway" version "6.1.4"
    id 'com.google.cloud.tools.jib' version '2.5.0'
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'club.jambuds'
version '0.0.1-SNAPSHOT'
mainClassName = "club.jambuds.ApplicationKt"

distZip {
    enabled = false
}

distTar {
    enabled = false
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'io.javalin:javalin:3.7.0'
    implementation "com.typesafe:config:1.4.0"
    implementation 'com.google.code.gson:gson:2.8.6'

    // email stuff
    implementation "io.pebbletemplates:pebble:3.1.4"
    implementation 'com.sendgrid:sendgrid-java:4.6.4'

    // validation
    implementation "org.hibernate.validator:hibernate-validator:6.1.2.Final"
    implementation "org.glassfish:javax.el:3.0.0"

    // third-party apis
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:4.2.1'
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.retrofit2:converter-scalars:2.7.1'
    // used for third party api auth (twitter, apple music)
    implementation "com.auth0:java-jwt:3.10.0"
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.64'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.64'

    // logging & monitoring
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation 'io.sentry:sentry:1.7.30'
    implementation 'io.sentry:sentry-logback:1.7.30'
    implementation 'com.newrelic.agent.java:newrelic-api:5.14.0'

    // database access
    implementation "org.jdbi:jdbi3-core:$jdbi3_version"
    implementation "org.jdbi:jdbi3-postgres:$jdbi3_version"
    implementation "org.jdbi:jdbi3-sqlobject:$jdbi3_version"
    implementation "org.jdbi:jdbi3-kotlin:$jdbi3_version"
    implementation "org.jdbi:jdbi3-kotlin-sqlobject:$jdbi3_version"
    implementation "org.postgresql:postgresql:42.2.9"
    implementation "com.zaxxer:HikariCP:3.4.2"
    implementation "io.lettuce:lettuce-core:5.2.2.RELEASE"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.0'
    testImplementation "com.konghq:unirest-java:3.4.03"
    testImplementation "org.flywaydb:flyway-core:6.1.4"
    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation group: 'com.nhaarman.mockitokotlin2', name: 'mockito-kotlin', version: '2.2.0'
}

test {
    environment "JAMBUDS_ENV", "test"
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

flyway {
    url = "jdbc:postgresql://localhost:5433/jambuds"
    user = "postgres"
    password = ""
}

jib {
    from {
        image = "azul/zulu-openjdk-alpine:11.0.6"
    }
    container {
        mainClass = "club.jambuds.ApplicationKt"
        // TODO: use runtime env var for this?
        jvmFlags = [
            "-XX:+UseG1GC",
            "-Xmx100m",
            "-XX:+UseStringDeduplication"
        ]
    }
}
